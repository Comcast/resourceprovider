import com.xfinity.rpplugin.ResourceProviderFactory

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

kapt {
    correctErrorTypes = true
    useBuildCache = false
}

ext {
    appId = 'com.xfinity.resourceprovider.sample'
}
android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId appId
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName rootProject.publishVersion
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.nhaarman:mockito-kotlin:1.6.0'

    api project(':library')
    kapt project(':compiler')

    testCompile 'junit:junit:4.12',
                'org.mockito:mockito-core:2.7.21',
                project(':testutils'),
                project(':library')

    testImplementation('com.nhaarman:mockito-kotlin:1.4.0',
                       {
                           exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
                           exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
                       })

    kaptTest project(':compiler')
}
repositories {
    mavenCentral()
}

task buildResourceProvider {
    dependsOn 'processDebugResources'
    doLast {
        String RClassPath = project.buildDir.toString() + "/intermediates/compile_and_runtime_not_namespaced_r_class_jar/debug/"
        exec {
            workingDir "$RClassPath"
            executable 'unzip'
            args 'R.jar'
            ignoreExitValue true
        }

        exec {
            workingDir "$RClassPath"
            commandLine "bash", "-c", "find $RClassPath -name '*.class' | xargs javap -p > rclass.txt"
        }

        def resourceProviderFactory = new ResourceProviderFactory()
        resourceProviderFactory.buildResourceProvider(appId, "${project.buildDir.toString()}/generated/source/kapt/debug")

        //When we're done Remove everything except  R.jar
//        exec {
//            workingDir "$RClassPath"
//            commandLin 'bash', "-c", "ls | grep -v R.jar | xargs rm -rf"
//        }
    }
}